param name string
@description('Primary location for all resources & Flex Consumption Function App')
param location string = resourceGroup().location
param tags object = {}
param applicationInsightsName string = ''
param appServicePlanId string
param appSettings object = {}

@allowed([
  'dotnet', 'dotnetcore', 'dotnet-isolated', 'node', 'python', 'java', 'powershell', 'custom'
])
param runtimeName string
param runtimeVersion string
var runtimeNameAndVersion = '${runtimeName}|${runtimeVersion}'
param serviceName string = 'appservice'
param storageAccountName string
param virtualNetworkSubnetId string = ''
param UserAssignedManagedIdentityId string = ''
param UserAssignedManagedIdentityClientId string = ''
param botUserAssignedManagedIdentityId string
param enableBlob bool = true
param enableQueue bool = false
param enableTable bool = false
param enableFile bool = false

@allowed(['SystemAssigned', 'UserAssigned'])
param identityType string

var applicationInsightsIdentity = identityType == 'UserAssigned'
  ? 'ClientId=${UserAssignedManagedIdentityClientId};Authorization=AAD'
  : 'Authorization=AAD'

// Create base application settings
var baseAppSettings = {
  // Application Insights settings are always included
  APPLICATIONINSIGHTS_AUTHENTICATION_STRING: applicationInsightsIdentity
  // https://learn.microsoft.com/en-us/azure/azure-monitor/app/codeless-app-service?tabs=aspnetcore#application-settings-definitions
  ApplicationInsightsAgent_EXTENSION_VERSION: '~3'
  XDT_MicrosoftApplicationInsights_Mode: 'recommended'
}

var userManagedIdentityStorageAccountSettings = identityType == 'UserAssigned'
  ? {
      AzureWebJobsStorage__credential: 'managedidentity'
      AzureWebJobsStorage__clientId: UserAssignedManagedIdentityClientId
    }
  : {}

// Dynamically build storage endpoint settings based on feature flags
var blobSettings = enableBlob ? { AzureWebJobsStorage__blobServiceUri: stg.properties.primaryEndpoints.blob } : {}
var queueSettings = enableQueue ? { AzureWebJobsStorage__queueServiceUri: stg.properties.primaryEndpoints.queue } : {}
var tableSettings = enableTable ? { AzureWebJobsStorage__tableServiceUri: stg.properties.primaryEndpoints.table } : {}
var fileSettings = enableFile ? { AzureWebJobsStorage__fileServiceUri: stg.properties.primaryEndpoints.file } : {}

// Merge all app settings
var allAppSettings = union(
  appSettings,
  blobSettings,
  queueSettings,
  tableSettings,
  fileSettings,
  baseAppSettings,
  userManagedIdentityStorageAccountSettings
)

resource stg 'Microsoft.Storage/storageAccounts@2022-09-01' existing = {
  name: storageAccountName
}

resource applicationInsights 'Microsoft.Insights/components@2020-02-02' existing = if (!empty(applicationInsightsName)) {
  name: applicationInsightsName
}

// Create a service web app
module appservice 'br/public:avm/res/web/site:0.19.3' = {
  name: '${serviceName}-web-app'
  params: {
    kind: 'app,linux'
    name: name
    location: location
    tags: union(tags, { 'azd-service-name': serviceName })
    serverFarmResourceId: appServicePlanId
    managedIdentities: {
      systemAssigned: identityType == 'SystemAssigned'
      userAssignedResourceIds: identityType == 'SystemAssigned'
        ? [botUserAssignedManagedIdentityId]
        : [
            '${UserAssignedManagedIdentityId}'
            '${botUserAssignedManagedIdentityId}'
          ]
    }
    
    siteConfig: {
      linuxFxVersion: runtimeNameAndVersion
      numberOfWorkers: 1
      alwaysOn: false
      cors: {
        allowedOrigins: [
          'https://portal.azure.com'
        ]
      }
    }
    httpsOnly: true
    clientCertEnabled: false
    clientCertMode: 'Required'
    // storageAccountRequired: false
    autoGeneratedDomainNameLabelScope: 'TenantReuse'
    virtualNetworkSubnetResourceId: !empty(virtualNetworkSubnetId) ? virtualNetworkSubnetId : null

    configs: [
      {
        name: 'appsettings'
        applicationInsightResourceId: applicationInsights.id
        // storageAccountResourceId: stg.id
        // storageAccountUseIdentityAuthentication: true
        properties: allAppSettings
      }
      {
        name: 'web'
        properties: {
          linuxFxVersion: runtimeNameAndVersion
        }
      }
    ]

    basicPublishingCredentialsPolicies: [
      {
        allow: false
        name: 'ftp'
      }
      {
        allow: false
        name: 'scm'
      }
    ]
  }
}

output serviceName string = appservice.outputs.name
// Ensure output is always string, handle potential null from module output if SystemAssigned is not used
output serviceIdentityPrincipalId string = identityType == 'SystemAssigned'
  ? appservice.outputs.?systemAssignedMIPrincipalId ?? ''
  : ''
output serviceDefaultHostName string = appservice.outputs.defaultHostname
